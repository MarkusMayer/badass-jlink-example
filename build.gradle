plugins {
    id 'org.beryx.jlink' version '1.1.3'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 10
targetCompatibility = 10

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'
}

mainClassName = 'org.beryx.modular.hello.Hello'
jar {
    manifest {
        attributes 'Implementation-Title': "modular-hello",
                'Main-Class': mainClassName
    }
}

compileJava {
    options.compilerArgs = ['--module-path', classpath.asPath]
    classpath = files()
}

jlink {
    launcherName = 'hello'
    mergedModule {
        requires 'java.naming';
        requires 'java.xml';
        requires 'java.xml.bind';
    }
    beforeZip {
        copy {
            from('src/main/resources')
            into("$buildDir/image/bin")
        }
        adjustStartScripts()
    }
}

def adjustStartScripts() {
    ['hello', 'hello.bat'].each { script ->
        def scriptFile = new File("$buildDir/image/bin/$script")
        if(scriptFile.file) {
            def newText = scriptFile.text.replace('JLINK_VM_OPTIONS=', 'JLINK_VM_OPTIONS="-Dlogback.configurationFile=./logback.xml"')
            scriptFile.newWriter().withWriter {w -> w << newText}
        }
    }
}
