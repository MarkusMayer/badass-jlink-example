plugins {
    id "org.javamodularity.moduleplugin" version "1.7.0"
    id "org.beryx.jlink" version "2.21.3"
}

repositories {
    mavenCentral()
}

sourceCompatibility = 14
targetCompatibility = 14

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
}

application {
    mainClass = 'org.beryx.modular.hello.Hello'
    mainModule = 'org.beryx.modular.hello'
    applicationDefaultJvmArgs += ['--enable-preview', '-Dlogback.configurationFile=./logback.xml']
}

jar {
    manifest {
        attributes 'Implementation-Title': "modular-hello",
                'Main-Class': 'org.beryx.modular.hello.Hello'
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

jlink {
    mergedModule {
        version = '5.5.5'
        requires 'java.naming'
        requires 'java.xml'
    }
    launcher{
        name = 'hello'
        jvmArgs = ['--enable-preview', '-Dlogback.configurationFile=./logback.xml']
    }
    jpackage {
        if(org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions += ['--win-console']
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
}

tasks.jlink.doLast {
    copy {
        from('src/main/resources')
        into("$buildDir/image/bin")
    }
}
